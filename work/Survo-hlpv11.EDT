SURVO 98 edit field:    201       4000       380 (32 bit version)
0001|*SAVE Survo-hlpv11 / Survo-help_v11 16.10.2016 Jouni Vatanen
0007|*/ACTIVATE +
0009|*CD S:\Muut\Survo-hlp
0012|*INDEX
S   |4
0013|* - CONVERT TO HTML
0014|* - CSS FILE
0015|* - SEARCH BAR
0016|* - WEB PAGE CHAPTERS
0017|* - PRINT WEB CONTENT
0019|*Important links
S   |3
0020|*Hard disk  ## /OPEN www\
0021|*Survo help ## /OPEN https://d3l3g8x9oq84ue.cloudfront.net
0022|*git(koodi) ## /OPEN https://github.com/JouniVatanen/Survo-help
0023| AWS(pilvi) ## /OPEN https://console.aws.amazon.com/quickstart-website/website/aws-website-survo-help-b1m87
0024|*Survo VAR  ## /OPEN https://www.google.fi/search?q=survo+var
0026|*Instructions
S   |3
0027|*To make a new command
0028|*1. Go to Command web pages from index.
0029|*2. Activate the copy to get you a base.
0030|*3. Add content to your choosing.
0031|* o Title of the web page in shadow 7
S   |                                    7
0032|* o Parameters and brief explanations where control column is I and
0033|*   explanation is N and the code is in shadow x
S   |                                               x
0034|* o Links in the following form 'name-of-the-command'.html for example
0035|*   file-save-with-fields.html
0036|*4. Be sure to mark shadows and control columns as in the examples.
0037|*See already made command pages for more advices.
0039|*Activate-command will make you
0040|*1. New folders under subfolder www/
0041|*2. Necessary HTML5.DEV and Q.DV2
0042|*3. Other content like humans.txt, robots.txt and possible js-files
0043|*4. Web pages
0045|*The web page to work you will also need to copy from zip-file images,
0046|*other media and bootstrap-files from folders css, fonts, img and js.
0048|*........................................................................................................................................................................................................
0049|*Activate next line to make the Survo help web pages
S   |1
0050|*/ACTIVATE +
0052|*Create libraries
S   |3
0053|+subfolder                                      ## MD www
0054|+webpage-root                                   ## MD www/public_html
0055|+css-files                                      ## MD www/public_html/css
0056|+fonts                                          ## MD www/public_html/fonts
0057|+images                                         ## MD www/public_html/img
0058|+other media                                    ## MD www/public_html/media
0059|+javascript                                     ## MD www/public_html/js
0060|+documents                                      ## MD www/doc
0061|+php-scripts etc.                               ## MD www/lib
0063|*Use Mklink in windows to create paths to folders where there are „,”,† or spaces
S   |3
0064|*Note: might not work in win8.1 or win10
0065|*Example: >mklink /j c:\home "c:\Users\Name\Some absurdly long name with scandic like „\Real folder"
0067|*Web page structure
S   |3
0068|*public_html/
0069|*  css/
0070|*  fonts/
0071|*  img/
0072|*  media/
0073|*  .htaccess
0074|*  index.html
0075|*  404.html
0076|*  index.php
0077|*doc/
0078|*lib/
0080|*Don't forget files
S   |3
0081|*.htaccess
0082|*index.php
0083|*apple-touch-icon.png
0084|*favicon.ico
0085|*tile.png
0086|*tile-wide.png
0087|*humans.txt
0088|*robots.tx
0089|*404.html
0090|*for more check e.g. html5 boilerplate
0092|*Used Bootstrap framework
S   |3
0093|*Read more ## /OPEN http://getbootstrap.com/
0095|*..........
0096|*List about Survo commands and parameters
S   |3
0097|*SHOW SurvoR_specs.txt
0098|*TXTCONV SurvoR_specs.txt,SurvoR_specsv2.txt
0099|*CONVERSIONS:
0100|*t ";char(32)/" "char(10)char(32)/"
0101|*t ";char(32)char(32)/" "char(10)char(32)char(32)/"
0102|*t ";char(32)" "----------"
0103|*t ";" "char(10)"
0104|*END
0105|*..........
0106|*TXTCONV Survo _specsv2.txt,SurvoR_specsv3.txt
0107|*CONVERSIONS:
0108|*t "----------" ";char(32)"
0109|*END
0111|*/OPEN SurvoR_specsv3.txt
0112|*SHOW SurvoR_specsv3.txt
0114|*Next think how to use the list
S   |1
0116|*........................................................................................................................................................................................................
0117|* - CONVERT TO HTML
S   |4
0118|*Webpage settings, changes in red
S   |3
0119|+SAVEP CUR+1,E>,Q.DV2
0120|* / Definitions for the HTML version of the inquiry system / kv 1999 & jv 2016
0121|* / Made responsive using Bootstrap
0122|* <!-- Bootstrap -->[LF]
0123|* <!-- HTML5 shim and Respond.js for IE8 support of HTML5 elements and media queries -->[LF]
0124|* <!-- WARNING: Respond.js doesn't work if you view the page via file:// -->[LF]
0125|* <!-- [LB]if lt IE 9[RB]>[LF]
0126|*   [SCRIPT] SRC="https://oss.maxcdn.com/html5shiv/3.7.3/html5shiv.min.js">[/SCRIPT][LF]
0127|*   [SCRIPT] SRC="https://oss.maxcdn.com/respond/1.4.2/respond.min.js">[/SCRIPT][LF]
0128|* <![LB]endif[RB]-->[LF]
0129|* <!-- Custom styles for this template -->[LF]
0130|* [STYLESHEET(css/bootstrap.min.css)][LF]
0131|* [STYLESHEET(css/main.css)][LF]
0132|* [META] NAME="description" content=""[/META][LF]
0133|* <link rel="author" href="humans.txt">[LF]
0134|* [SCRIPT][LF]
0135|*  (function() {[LF]
0136|*    var cx = '010896179039517952376:nay7kuxzphw';[LF]
0137|*    var gcse = document.createElement('script');[LF]
0138|*    gcse.type = 'text/javascript';[LF]
0139|*    gcse.async = true;[LF]
0140|*    gcse.src = 'https://cse.google.com/cse.js?cx=' + cx;[LF]
0141|*    var s = document.getElementsByTagName('script')[LB]0[RB];[LF]
0142|*    s.parentNode.insertBefore(gcse, s);[LF]
0143|*  })();[LF]
0144|* [/SCRIPT][LF]
0145|* define [pict1] [IMAGE(../img/survologo1.gif)]
0146|* define [pict2] WIDTH="200"[S]HEIGHT="82"[S]ALT="SURVO&nbsp;MM"[/IMAGE]
0147|* define [Picture] [pict1][pict2]
0148|* / [H1][Picture]&nbsp;[LINK(mainmenu.html)][Name][/LINK][/H1]
0149|* / -----------------------------------------------------------------
0150|* / define [Syntax] [H4]
0151|* / define [/Syntax] [/H4][RULER]
0152|* define [HelpText] [LF][P(HelpText)]
S   |1
0153|* define [/HelpText] [/P][RULER]
S   |1
0154|* define [Example] [%=0] / [RULER(70%)]
0155|* define [/Example] [%=0] / [RULER(70%)]
0156|* define [New] [%=0] / [STRONG]
0157|* define [/New] [%=0] / [/STRONG]
0158|* define [Mark] <B>
0159|* define [/Mark] </B>
0160|* define [Banner1] [LF][DIVCLASS(Details)][SMALL]
0161|* define [URL] [LINK(http://www.survo.fi/)]
0162|* define [More] More[S]information[S]on[S]Survo[S]from[S]
0163|* define [lt] www.survo.fi
0164|* define [SurvoLink] [More][URL][lt][/LINK][/SMALL]
0165|* define [SurSys] Survo[S]Systems[S]2001-2012
0166|* define [wm1] [MAIL(webmaster'at'survo.fi)]
0167|* define [wm2]       webmaster'at'survo.fi[/MAIL]
0168|* define [wmail] [wm1][wm2]
0169|* define [b2a] [SMALL]Copyright[Copyright]
0170|* define [b2b] [SurSys].[+][wmail][/SMALL][/DIVCLASS]
0171|* define [Banner2] [b2a][b2b]
0172|* / control E: [/HelpText][Banner1][SurvoLink][+][Banner2][LF][END]
S   |1
0173|* shadow /: [STRONG] [/STRONG]
0174|* shadow f: [Mark] [/Mark]     / (fixed footnotes)
0175|* shadow 7: [Mark] [/Mark]
0176|* shadow 8: [New] [/New]
0177|* shadow 4: [Mark] [/Mark]
0178|* shadow 2: [%=0]
0179|* shadow 3: [%=0]
0180|* / shadow E: [Mark] [/Mark]
S   |1
0181|* shadow ?: [Mark] [/Mark]
0182|* shadow _: [Mark] [/Mark]
0183|* shadow N: [Mark] [/Mark]
0184|* shadow L: [Mark] [/Mark]
0185|* shadow D: [Mark] [/Mark]
0186|* shadow ,: [Mark] [/Mark]
0187|* shadow *: [Mark] [/Mark]
0188|* shadow {: [Mark] [/Mark]
0189|* shadow }: [Mark] [/Mark]
0190|* shadow z: [Mark] [/Mark]
0191|* shadow w: [Mark] [/Mark]
0192|* / shadow x: [Mark] [/Mark]
S   |1
0193|* shadow |: [%=0] [/LINK]
0194|* shadow ;: [%=0] [/LINK][/New]
0195|* control #: [%=0]
0196|* control ": [%=0]
0197|* control [LB]: [Example]
0198|* control [S]: [S][S][S]
0199|* control [RB]: [/Example]
0200|* shadow +: [Mark] [/Mark]
0201|* / control N: [%=0]
S   |1
0202|* replace ................. ................. / no replace, that is
0203|* /
0204|E
0206|*HTML50 settings, changes in red
S   |3
0207|+SAVEP CUR+1,E>,HTML50.DEV
0208|* / HTML50.DEV / Survo "printer driver" for HTML  9.3.1995/K.Vehkalahti
0209|* / HTML 4.0 24.7.1998/kv
0210|* / Some CSS definitions 24.2.1999/kv
0211|* / Deprecated elements marked 7.4.1999/kv
0212|* / SURVO MM driver 24.7.2000/kv
0213|* / Image positioning tags + minor enhancements 14.2.2001/kv
0214|* / HTML 4.01 w. dtd 30.8.2001/kv
0215|* / HTML 5 26.7.2016/jv
S   |1
0216|* / -------------------------------------------------------------------
0217|* / This driver outputs HTML5 under SURVO.
0218|* / Usage:
0219|* / *PRINT CUR+1,E TO mypage.html
0220|* / - include HTML40.DEV
0221|* / * ...
0222|* / E
0223|* / -------------------------------------------------------------------
0224|* / Necessary definitions required for a Survo printer driver:
0225|* / -------------------------------------------------------------------
0226|* define [S]         [2/0]
0227|* define [LF]       [0/10]
0228|* define [NEL]        [LF]
0229|* define [LB]       [5/11]
0230|* define [RB]       [5/13]
0231|* define [TEXT]      [%=0]
0232|* define [FORM_FEED] [%=0]
0233|* define [NEW_PAGE]  [%=0]
0234|* define [NEWLINE]   [%=0]
0235|* shadow [S]:        [%=0]
0236|* / -------------------------------------------------------------------
0237|* / Actual definitions corresponding to the HTML codes:
0238|* / -------------------------------------------------------------------
0239|* define    [TITLE]    <TITLE>
0240|* define   [/TITLE]   </TITLE>
0241|* shadow 7: [TITLE]   [/TITLE]
0242|* define    [H1]       <H1>
0243|* define   [/H1]      </H1>
0244|* define    [H2]       <H2>
0245|* define   [/H2]      </H2>
0246|* define    [H3]       <H3>
0247|* define   [/H3]      </H3>
0248|* define    [H4]       <H4>
0249|* define   [/H4]      </H4>
0250|* define    [H5]       <H5>
0251|* define   [/H5]      </H5>
0252|* define    [H6]       <H6>
0253|* define   [/H6]      </H6>
0254|* define    [HEADER1]  [H1]
0255|* define   [/HEADER1] [/H1]
0256|* define    [HEADER2]  [H2]
0257|* define   [/HEADER2] [/H2]
0258|* define    [HEADER3]  [H3]
0259|* define   [/HEADER3] [/H3]
0260|* define    [HEADER4]  [H4]
0261|* define   [/HEADER4] [/H4]
0262|* define    [HEADER5]  [H5]
0263|* define   [/HEADER5] [/H5]
0264|* define    [HEADER6]  [H6]
0265|* define   [/HEADER6] [/H6]
0266|* shadow j: [HEADER1] [/HEADER1]
0267|* shadow k: [HEADER2] [/HEADER2]
0268|* shadow l: [HEADER3] [/HEADER3]
0269|* shadow m: [HEADER4] [/HEADER4]
0270|* shadow n: [HEADER5] [/HEADER5]
0271|* shadow o: [HEADER6] [/HEADER6]
0272|* define    [P]        <P>
0273|* define   [/P]       </P>
0274|* define    [/]        [P]
0275|* define    [/(align)] <P[S]ALIGN="align"> / deprecated
0276|* shadow /: [%=0]      [/]
0277|* control /:           [/]
0278|* define    [+]        <BR>
0279|* define    [+(clear)] <BR[S]CLEAR="clear">
0280|* shadow \: [+]
0281|* control +:           [+]
0282|* define    [RULER(x)] <HR[S]WIDTH="x"> / deprecated
0283|* define    [RULER]    <HR>
0284|* control =:           [RULER]
0285|* control |:           [+][+][RULER][+][+]
0286|* define  [DIV(x)]      <DIV[S]ALIGN="x"> / deprecated
0287|* define [/DIV]        </DIV>
0288|* define  [DIVISION(x)] [DIV(x)]
0289|* define [/DIVISION]   [/DIV]
0290|* define  [CENTER]      [DIV(center)] / deprecated
0291|* define [/CENTER]     [/DIV] / deprecated
0292|* control {:       [CENTER] / deprecated
0293|* control }:      [/CENTER] / deprecated
0294|* shadow {:        [CENTER] / deprecated
0295|* shadow }: [%=0] [/CENTER] / deprecated
0296|* define [NAV]          <NAV>
S   |1
0297|* define [NAV(x)]       <NAV[S]CLASS="x">
S   |1
0298|* define [NAV_DEF]      <NAV[S]
S   |1
0299|* define [/NAV_DEF]     >
S   |1
0300|* define [/NAV]        </NAV>
S   |1
0301|* define  [STRONG]      <STRONG>
0302|* define [/STRONG]     </STRONG>
0303|* define  [SUB]         <SUB>
0304|* define [/SUB]        </SUB>
0305|* define  [SUP]         <SUP>
0306|* define [/SUP]        </SUP>
0307|* define  [DFN]         <DFN>
0308|* define [/DFN]        </DFN>
0309|* define  [DEFINITION]  [DFN]
0310|* define [/DEFINITION] [/DFN]
0311|* define  [EM]          <EM>
0312|* define [/EM]         </EM>
0313|* define  [EMPHASIS]    [EM]
0314|* define [/EMPHASIS]   [/EM]
0315|* define  [CITE]        <CITE>
0316|* define [/CITE]       </CITE>
0317|* define  [CITATION]    [CITE]
0318|* define [/CITATION]   [/CITE]
0319|* define  [CODE]        <CODE>
0320|* define [/CODE]       </CODE>
0321|* define  [PRE]         <PRE>
0322|* define [/PRE]        </PRE>
0323|* define  [BLOCKQUOTE]  <BLOCKQUOTE>
0324|* define [/BLOCKQUOTE] </BLOCKQUOTE>
0325|* define  [Q]           <Q>
0326|* define [/Q]          </Q>
0327|* define  [DEL]         <DEL>
0328|* define [/DEL]        </DEL>
0329|* define  [SMALL]      <SMALL> / deprecated
0330|* define [/SMALL]     </SMALL> / deprecated
0331|* define  [BIG]        <BIG> / deprecated
0332|* define [/BIG]       </BIG> / deprecated
0333|* shadow 1: [STRONG]     [/STRONG]
0334|* shadow 2: [SUB]        [/SUB]
0335|* shadow 3: [SUP]        [/SUP]
0336|* shadow 4: [DEFINITION] [/DEFINITION]
0337|* shadow 6: [EMPHASIS]   [/EMPHASIS]
0338|* shadow 9: [CITATION]   [/CITATION]
0339|* shadow x: [CODE]       [/CODE]
0340|* shadow (: [CODE]
0341|* shadow ): [/CODE]
0342|* control (:   [PRE][CODE]
0343|* control ): [/CODE][/PRE]
0344|* control <:  [BLOCKQUOTE]
0345|* control >: [/BLOCKQUOTE]
0346|* define  [ADDRESS]  <ADDRESS>
0347|* define [/ADDRESS] </ADDRESS>
0348|* shadow 8: [LF][ADDRESS] [/ADDRESS]
0349|* control [LB]:  [ADDRESS]
0350|* control [RB]: [/ADDRESS]
0351|* shadow <: &lt  / The characters  < > &  MUST be painted with the corresponding
S   |1
0352|* shadow >: &gt   / shadow characters, since the code conversion (HTML.BIN) will
S   |1
0353|* shadow &: &amp  / remove them. Extra spaces can be removed e.g. by TXTCONV.
0354|* define [Copyright] [S]&copy;[S]
0355|* / Links:
0356|* define [MADEBY(x)] <LINK[S]REV=MADE[S]HREF="mailto:x">
0357|* define  [MAIL(x)]  <A[S]HREF="mailto:x">
0358|* define [/MAIL]    </A>
0359|* define  [NAME(x)]  <A[S]NAME="x">
0360|* define [/NAME]    </A>
0361|* define  [JUMP(x)]  <A[S]HREF="#x">
0362|* define [/JUMP]    </A>
0363|* define  [LINK(x)]  <A[S]HREF="x">
0364|* define [/LINK]    </A>
0365|* define [LINK_DEF] <A[S]
0366|* define [/LINK_DEF] >
0367|* shadow |:   [%=0] </A>
0368|* / Images:
0369|* define  [IMAGE(URL)]       <IMG[S]SRC="URL"[S]
0370|* define  [IMAGE_LEFT(URL)]  [IMAGE(URL)]ALIGN="LEFT"[S]
0371|* define  [IMAGE_RIGHT(URL)] [IMAGE(URL)]ALIGN="RIGHT"[S]
0372|* define  [IMAGE]            <IMG[S]
S   |1
0373|* define [/IMAGE] >
0374|* / Miscellaneous header codes:
0375|* define  [META]        <META[S]
0376|* define [/META]         >
0377|* define  [SCRIPT]      <SCRIPT>
S   |1
0378|* define [SCRIPT(x)]    <SCRIPT[S]src="x">
S   |1
0379|* define [/SCRIPT]      </SCRIPT>
S   |1
0380|* define  [GS(x)]       <GCSE:x>
S   |1
0381|* define [/GS(x)]       </GCSE:x>
S   |1
0382|* define  [DESCRIPTION] [META]NAME="description"[S]CONTENT="
0383|* define [/DESCRIPTION] ">
0384|* define  [KEYWORDS]    [META]NAME="keywords"[S]CONTENT="
0385|* define [/KEYWORDS]    ">
0386|* shadow ': [%=0] ">
0387|* / Body building:
0388|* define  [BODY]     </HEAD>[LF]<BODY>[LF]
0389|* define  [BODY_DEF] </HEAD>[LF]<BODY[S]
0390|* define [/BODY_DEF] >[LF]
0391|* define [END]       </BODY>[LF]</HTML>
0392|* control E: [LF][END][LF] / end of the document
0393|* / Shortcuts for basic backgrounds (deprecated):
0394|* define [Black]  "#000000"
0395|* define [White]  "#FFFFFF"
0396|* define [WhiteBody] </HEAD>[LF]<BODY[S]BGCOLOR=[White][S]TEXT=[Black]> / deprecated
0397|* define [BlackBody] </HEAD>[LF]<BODY[S]BGCOLOR=[Black][S]TEXT=[White]> / deprecated
0398|* / Lists:
0399|* define   [LIST]     <UL>
0400|* define  [/LIST]    </UL>
0401|* define  [NLIST]     <OL>
0402|* define [/NLIST]    </OL>
0403|* define  [NLIST(x)]  <OL[S]TYPE="x">
0404|* define     [L]         <LI>
0405|* define     [L(x)]      <LI[S]TYPE="x">
0406|* define     [/L]        </LI>
S   |1
0407|* shadow L:  [L]
0408|* control L: [L]         [/L]
S   |1
0409|* define  [DLIST]     <DL>
0410|* define  [DLIST(x)]  <DL[S]COMPACT> / deprecated
0411|* define [/DLIST]    </DL>
0412|* define     [T]      <DT>
0413|* shadow I:  [T]
0414|* control I: [T]
0415|* define     [D]         <DD>
0416|* shadow N:  [D]
0417|* control N: [D]
0418|* / Fonts (ALL deprecated, use CSS instead):
0419|* define    [FONT_SIZE(size)]   <FONT[S]SIZE="size"> / deprecated
0420|* define    [FONT_COLOR(color)] <FONT[S]COLOR="color"> / deprecated
0421|* define    [FONT(size,color)]  <FONT[S]SIZE="size"[S]COLOR="color"> / deprecated
0422|* define   [/FONT]             </FONT> / deprecated
0423|* shadow +: [FONT_SIZE(+1)]    [/FONT] / deprecated
0424|* shadow -: [FONT_SIZE(-1)]    [/FONT] / deprecated
0425|* / Tables:
0426|* define  [TABLE]         <TABLE>
0427|* define  [TABLE(BORDER)] <TABLE[S]BORDER>
0428|* define  [TABLE_DEF]     <TABLE[S]
0429|* define [/TABLE_DEF]     >
0430|* define [/TABLE]        </TABLE>
0431|* define  [TABLE_ROW]         <TR[S]
0432|* define [/TABLE_ROW]         >
0433|* control r:              <TR>
0434|* shadow r:               <TR>
0435|* control R:              [TABLE_ROW]ALIGN="right"[/TABLE_ROW]
0436|* define  [TABLE_HEADER]      <TH
0437|* define [/TABLE_HEADER]      >
0438|* define  [TABLE_HEADER(x,y)] <TH[S]ROWSPAN="x"[S]COLSPAN="y">
0439|* shadow H: [%=0] [TABLE_HEADER]ALIGN="left"[/TABLE_HEADER]
0440|* define  [TABLE_CELL]        <TD[S]
0441|* define [/TABLE_CELL]        >
0442|* define  [TABLE_CELL(x,y)]   <TD[S]ROWSPAN="x"[S]COLSPAN="y">
0443|* shadow e: [%=0] [TABLE_CELL]ALIGN="right"[/TABLE_CELL]
0444|* shadow E: [%=0] [TABLE_CELL]ALIGN="left"[/TABLE_CELL]
0445|* define    [TABLE_CAPTION]        <CAPTION>
0446|* define    [TABLE_CAPTION(align)] <CAPTION[S]ALIGN="align"> / deprecated
0447|* define   [/TABLE_CAPTION]       </CAPTION>
0448|* shadow ": [TABLE_CAPTION] [/TABLE_CAPTION]
0449|* / Forms:
0450|* define  [FORM(x,y)] <FORM[S]METHOD="x"[S]ACTION="y">
0451|* define [FORM]      <FORM>
S   |1
0452|* define [/FORM]     </FORM>
0453|* define  [FORM_DEF]  <FORM[S]
0454|* define [/FORM_DEF]  >
0455|* define  [INPUT(x,y)] <INPUT[S]NAME="x"[S]TYPE="y"[S]
0456|* define [/INPUT]     >
0457|* define  [SELECT_DEF(name)] <SELECT[S]NAME="name"[S]
0458|* define [/SELECT_DEF]       >
0459|* define  [SELECT(name)]     [SELECT_DEF(name)][/SELECT_DEF]
0460|* define [/SELECT]          </SELECT>
0461|* define    [OPTION]   <OPTION>
0462|* shadow O: [OPTION]
0463|* define    [SELECTED] <OPTION[S]SELECTED>
0464|* shadow S: [SELECTED]
0465|* define  [TEXT_DEF(name)] <TEXTAREA[S]NAME="name"[S]
0466|* define [/TEXT_DEF]       >
0467|* define  [TEXT(name)]     [TEXT_DEF(name)][/TEXT_DEF]
0468|* define [/TEXT]          </TEXTAREA>
0469|* / Miscellaneous:
0470|* shadow !: [%=0] >
0471|* shadow ?: [LF]<!--[S] [S]-->[LF]
0472|* / Maps and areas:
0473|* define [MAP(name)] <MAP[S]NAME="name">[LF]
0474|* define [/MAP] </MAP>[LF]
0475|* define [AREA(URL)] <AREA[S]HREF="URL"[S]
0476|* define [/AREA] >
0477|* / Frames: (7/98) (not a part of the DTD given below, not recommended)
0478|* define  [FRAMES_DEF]  <FRAMESET[S]
0479|* define [/FRAMES_DEF]  >
0480|* define [/FRAMES]     </FRAMESET>
0481|* define  [FRAME_DEF(URL,name)] <FRAME[S]SRC="URL"[S]NAME="name"[S]
0482|* define [/FRAME_DEF]          >
0483|* define  [NOFRAMES]    <NOFRAMES>
0484|* define [/NOFRAMES]   </NOFRAMES>[LF]</HTML>
0485|* define [/HEAD]       </HEAD>
0486|* define [LINK2(URL,frame)] <A[S]HREF="URL"[S]TARGET="frame">
0487|* define [LINK3(URL)]  [D]<A[S]HREF="URL">[T]
0488|* define [FOOTER]      <FOOTER>
S   |1
0489|* define [/FOOTER]     </FOOTER>
S   |1
0490|* define [IFRAME(URL,CLASS)] <IFRAME[S]SRC="URL"[S]CLASS="CLASS">
S   |1
0491|* define [/IFRAME]     </IFRAME>
S   |1
0492|* / Cascading Style Sheets: (2/99)
0493|* define  [STYLESHEET(URL)] <LINK[S]REL="stylesheet"[S]HREF="URL"[S]TYPE="text/css">[LF]
0494|* define  [STYLE]               <STYLE[S]TYPE="text/css">[LF][S][S]<!--[LF]
0495|* define [/STYLE] [S][S]-->[LF]</STYLE>[LF]
0496|* define [SPAN_DEF]  <SPAN[S]
S   |1
0497|* define [/SPAN_DEF]  >
S   |1
0498|* define  [SPAN(x)] <SPAN[S]CLASS="x">
0499|* define [/SPAN]   </SPAN>
0500|* / Classified elements: (CSS) (4/99)
0501|* define [DIV(x)]              <DIV[S]CLASS="x">
S   |1
0502|* define [DIVID(x)]            <DIV[S]ID="x">
S   |1
0503|* define [DIV_DEF]             <DIV[S]
S   |1
0504|* define [/DIV_DEF]             >
S   |1
0505|* define [LIST(x)]             <UL[S]CLASS="x">
S   |1
0506|* define [LIST_DEF]            <UL[S]
S   |1
0507|* define [/LIST_DEF]            >
S   |1
0508|* define [NLIST(x)]            <OL[S]CLASS="x">
S   |1
0509|* define [NLIST_DEF]           <OL
S   |1
0510|* define [/NLIST_DEF]           >
S   |1
0511|* define [L(x)]                <LI[S]CLASS="x">
S   |1
0512|* define [L_DEF]               <LI[S]
S   |1
0513|* define [/L_DEF]               >
S   |1
0514|* define [BUTTON(x)]           <BUTTON[S]CLASS="x">
S   |1
0515|* define [/BUTTON]             </BUTTON>
S   |1
0516|* define [BUTTON_DEF]          <BUTTON[S]
S   |1
0517|* define [/BUTTON_DEF]          >
S   |1
0518|* define [FOOTER(x)]           <FOOTER[S]CLASS="x">
S   |1
0519|* define [CODE(x)]             <CODE[S]CLASS="x">
0520|* define [STRONG(x)]         <STRONG[S]CLASS="x">
0521|* define [EM(x)]                 <EM[S]CLASS="x">
0522|* define [PRE(x)]               <PRE[S]CLASS="x">
0523|* define [H1(x)]                 <H1[S]CLASS="x">
0524|* define [H2(x)]                 <H2[S]CLASS="x">
0525|* define [H3(x)]                 <H3[S]CLASS="x">
0526|* define [H4(x)]                 <H4[S]CLASS="x">
0527|* define [H5(x)]                 <H5[S]CLASS="x">
0528|* define [H6(x)]                 <H6[S]CLASS="x">
0529|* define [CITE(x)]             <CITE[S]CLASS="x">
0530|* define [P(x)]                   <P[S]CLASS="x">
0531|* define [BLOCKQUOTE(x)] <BLOCKQUOTE[S]CLASS="x">
0532|* define [CITATION(x)] [CITE(x)]
0533|* define [EMPHASIS(x)] [EM(x)]
0534|* define [HEADER1(x)]  [H1(x)]
0535|* define [HEADER2(x)]  [H2(x)]
0536|* define [HEADER3(x)]  [H3(x)]
0537|* define [HEADER4(x)]  [H4(x)]
0538|* define [HEADER5(x)]  [H5(x)]
0539|* define [HEADER6(x)]  [H6(x)]
0540|* define [DESC]        [H3]Description[/H3]
S   |1
0541|* define [USAGE]       [H3]Usage[/H3]
S   |1
0542|* define [PARAMETERS]  [H3]Parameters[/H3]
S   |1
0543|* define [DETAILS]     [H3]Details[/H3]
S   |1
0544|* define [LINKS]       [H3]See[S]also[/H3]
S   |1
0545|* define [EXAMPLES]    [H3]Examples[/H3]
S   |1
0546|* / End of definitions; remove border lines and convert to ISO-Latin-1
0547|* / 18.2.1999: remove   < > &   altogether (use corresponding shadows!)
0548|* replace .................
0549|* <!DOCTYPE html>[LF]
S   |1
0550|* <html lang="en">[LF]
S   |1
0551|* <HEAD>[LF]
S   |1
0552|* <META charset="utf-8">[LF]
S   |1
0553|* <META http-equiv="X-UA-Compatible" content="IE=edge">[LF]
S   |1
0554|* <META name="viewport" content="width=device-width, initial-scale=1">
S   |1
0555|* <!-- The above 3 meta tags *must* come first in the head; any other head content must come *after* these tags -->[LF]
S   |1
0556|* codes HTML50.BIN
S   |1
0557|E
0559|*........................................................................................................................................................................................................
0560|* - CSS FILE
S   |4
0561|*Check bootstrap!
S   |3
0562|*Search bar, menu and responsiveness
S   |3
0564|+SAVEP CUR+2,E> www\public_html\css\main.css
0565|*/OPEN www\public_html\css\main.css
0569|*/*Fix Problems with Google Custom Seach Engine CSS*/
0570|*.reset-box-sizing, .reset-box-sizing *, .reset-box-sizing *:before, .reset-box-sizing *:after,  .gsc-inline-block {
0571|*  -webkit-box-sizing: content-box;
0572|*  -moz-box-sizing: content-box;
0573|*   box-sizing: content-box;
0574|*}
0575|*input.gsc-input, .gsc-input-box, .gsc-input-box-hover, .gsc-input-box-focus, .gsc-search-button {
0576|*  box-sizing: content-box;
0577|*  line-height: normal;
0578|*}
0580|*/*Logo at navbar shows correctly*/
0581|*.navbar-brand {
0582|*  padding: 0px; /* firefox bug fix */
0583|*}
0584|*.navbar-brand>img {
0585|*  height: 100%;
0586|*  padding: 10px; /* firefox bug fix */
0587|*  width: auto;
0588|*}
0590|*/*Navbar items widths at tablet size is smaller*/
0591|*@media (max-width: 992px) {
0592|*  .nav > li > a {
0593|*    padding-left:10px;
0594|*    padding-right:10px;
0595|*  }
0596|*}
0598|*/*Needed for fixed navbar to show all content*/
0599|*body {
0600|*  padding-top: 60px;
0601|*}
0602|*@media (max-width: 768px) {
0603|*  body {
0604|*    padding-top: 120px;
0605|*  }
0606|*}
0608|*/*Search bar Google logo is required*/
0609|*.form-control {
0610|*  padding:5px 10px;
0611|*  background-image:url("../img/google-custom-search-watermark.gif");
0612|*  background-repeat:no-repeat;
0613|*  background-position:2px;
0614|*  color:transparent;
0615|*}
0616|*.form-control:focus {
0617|*  background-image:none;color:black;
0618|*}
0620|*/*Brand: colors and fonts*/
0621|*BODY {
0622|*  font-family: "Helvetica Neue", Helvetica, Arial, sans-serif;
0623|*  color: #001030;
0624|*}
0625|*PRE CODE {
0626|*  font-family: "Lucida Console", Monaco, monospace;
0627|*}
0628|*H1 {
0629|*  font-family: Arial, Verdana, sans-serif;
0630|*  font-size: x-large;
0631|*  font-weight: 700;
0632|*  margin: 0;
0633|*  padding: 0;
0634|*  color: #006991;
0635|*  background: transparent none;
0636|*}
0637|*H2 {
0638|*  font-family: Arial, Verdana, sans-serif;
0639|*  font-size:large;
0640|*  font-weight: 700;
0641|*  margin: 0.9em 0 0.3em 0;
0642|*  padding: 0;
0643|*  color: #006991;
0644|*  background: transparent none;
0645|*  text-transform: uppercase;
0646|*}
0647|*H3 {
0648|*  font-family: Arial, Verdana, sans-serif;
0649|*  font-size: medium;
0650|*  font-weight: 700;
0651|*  margin: 0.9em 0 0.3em 0;
0652|*  padding: 0;
0653|*  color: #666666;
0654|*  background: transparent none;
0655|*}
0656|*H4 {
0657|*  font-family: Arial, Verdana, sans-serif;
0658|*  font-size: medium;
0659|*  font-weight: 700;
0660|*  margin: 0.6em 0 0 0;
0661|*  padding: 0;
0662|*  color: #006991;
0663|*  background: transparent none;
0664|*}
0666|*/*List for Parameters*/
0667|*dl {
0668|*  padding: 0 0 0 0;
0669|*}
0670|*dt {
0671|*  padding: 0.8em 0 0 0;
0672|*}
0673|*dd {
0674|*  margin: 0 0 0.1em 1em;
0675|*}
0679|E
0681|*........................................................................................................................................................................................................
0682|* - SEARCH BAR
S   |4
0683|*Look examples
S   |3
0684|*Basic example - part1        ## /OPEN http://markonphp.com/autocomplete-php-jquery-mysql-part1/
0685|*Basic example - part2        ## /OPEN http://markonphp.com/autocomplete-php-jquery-mysql-part2/
0686|*Basic example - part3        ## /OPEN http://markonphp.com/autocomplete-php-jquery-mysql-part3/
0687|*About autocomplete           ## /OPEN http://www.pontikis.net/blog/jquery-ui-autocomplete-step-by-step
0689|*Code examples
S   |3
0690|*Basic example - partial code ## /OPEN http://markonphp.com/files/auto-complete-tutorial_part1.zip
0691|*Basic example - full code    ## /OPEN http://markonphp.com/files/auto-complete-tutorial_part2.zip
0692|*Another example - full code  ## /OPEN http://www.w3schools.com/php/php_ajax_livesearch.asp
0693|*jQuery autocomplete          ## /OPEN https://jqueryui.com/autocomplete/
0694|*Protect from SQL-injection   ## /OPEN http://www.webreference.com/programming/php/search/3.html
0696|*Quick version powered by GoogleSearch
S   |3
0697|*Add - this code below the textbox in HTML menu
0698|*Add - in html inside form id="frmSearch"
0699|*Add - in html inside input id="txtSearch"
0701|*<script type="text/javascript">
0702|*    document.getElementById('frmSearch').onsubmit = function() {
0703|*        window.location = 'http://www.google.com/search?q=site:www.survo.fi/help/ ' + document.getElementById('txtSearch').value;
0704|*        return false;
0705|*    }
0706|*</script>
0708|*Advanced version: PHP, Javascript, jQuery, Autofill, MySQL
S   |3
0709|*constant.php
0710|*database.php
0711|*auto-complete.js
0712|*auto-complete.php
0714|*Part1: auto-complete.php v1
S   |3
0715|*SAVEP CUR+2,E> www\public_html\auto-complete.php
0717|*<?php
0718|*$data = array("alpaca", "buffalo", "cat", "tiger");
0719|*echo json_encode($data);
0720|*?>
0721|E
0722|*Part2: auto-complete.js (works!)
S   |3
0723|*SAVEP CUR+2,E> www\public_html\auto-complete.js
0725|*var MIN_LENGTH = 3;
0727|*$( document ).ready(function() {
0728|*          $("#keyword").keyup(function() {
0729|*                    var keyword = $("#keyword").val();
0730|*                    if (keyword.length >= MIN_LENGTH) {
0731|*                              $.get( "auto-complete.php", { keyword: keyword } )
0732|*                              .done(function( data ) {
0733|*                                        $('#results').html('');
0734|*                                        var results = jQuery.parseJSON(data);
0735|*                                        $(results).each(function(key, value) {
0736|*                                                  $('#results').append('<div class="item">' + value + '</div>');
0737|*                                        })
0739|*                                  $('.item').click(function() {
0740|*                                        var text = $(this).html();
0741|*                                        $('#keyword').val(text);
0742|*                                  })
0744|*                              });
0745|*                    } else {
0746|*                              $('#results').html('');
0747|*                    }
0748|*          });
0750|*    $("#keyword").blur(function(){
0751|*                    $("#results").fadeOut(500);
0752|*          })
0753|*        .focus(function() {
0754|*              $("#results").show();
0755|*          });
0757|*});
0758|E
0763|*Part3: link to jQuery in server (option: add to own server)
S   |3
0764|*http://code.jquery.com/jquery-2.1.1.min.js (version 3.0. exists)
0765|*SAVEP CUR+2,E> www\public_html\jquery.min.js
0767|*Is empty
S   |2
0768|E
0769|*Part4: constant.php
S   |3
0770|*SAVEP CUR+2,E> www\public_html\constant.php
0772|*<?php
0773|*// Database constants
0774|*define('DB_SERVER', "localhost");
0775|*define('DB_USER', "root");
0776|*define('DB_PASSWORD', "123");
0777|*define('DB_DATABASE', "countries");
0778|*define('DB_DRIVER', "mysql");
0779|*?>
0780|E
0781|*Part5: database.php
S   |3
0782|*SAVEP CUR+2,E> www\public_html\database.php
0784|*<?php
0786|*// We will use PDO to execute database stuff.
0787|*// This will return the connection to the database and set the parameter
0788|*// to tell PDO to raise errors when something bad happens
0789|*function getDbConnection() {
0790|*  $db = new PDO(DB_DRIVER . ":dbname=" . DB_DATABASE . ";host=" . DB_SERVER, DB_USER, DB_PASSWORD);
0791|*  $db->setAttribute(PDO::ATTR_ERRMODE, PDO::ERRMODE_EXCEPTION);
0792|*  return $db;
0793|*}
0796|*// This is the 'search' function that will return all possible rows starting with the keyword sent by the user
0797|*function searchForKeyword($keyword) {
0798|*    $db = getDbConnection();
0799|*    $stmt = $db->prepare("SELECT name_en as country FROM `countries` WHERE name_en LIKE ? ORDER BY country");
0801|*    $keyword = $keyword . '%';
0802|*    $stmt->bindParam(1, $keyword, PDO::PARAM_STR, 100);
0804|*    $isQueryOk = $stmt->execute();
0806|*    $results = array();
0808|*    if ($isQueryOk) {
0809|*      $results = $stmt->fetchAll(PDO::FETCH_COLUMN);
0810|*    } else {
0811|*      trigger_error('Error executing statement.', E_USER_ERROR);
0812|*    }
0814|*    $db = null;
0816|*    return $results;
0817|*}
0818|*?>
0819|E
0820|*Part 6: auto-complete.php
S   |3
0821|*SAVEP CUR+2,E> www\public_html\auto-complete.php
0823|*<?php
0824|*require('constant.php');
0825|*require('database.php');
0827|*if (!isset($_GET['keyword'])) {
0828|*          die();
0829|*}
0831|*$keyword = $_GET['keyword'];
0832|*$data = searchForKeyword($keyword);
0833|*echo json_encode($data);
0834|*?>
0835|E
0836|*Part 7: index.php
S   |3
0837|*Here comes the connection to other php files
0838|*SAVEP CUR+2,E> www\public_html\index.php
0840|*<?php
0844|*?>
0845|*........................................................................................................................................................................................................
0846|* - WEB PAGE CHAPTERS
S   |4
0847|*Menu
S   |3
0848|aDEF menu,a+1,A
0849|- include Q.DV2
0850|- [BODY]
0851|- [NAV_DEF]
0852|*CLASS="navbar navbar-default navbar-fixed-top"
0853|- [/NAV_DEF][DIV(container)][DIV(row)][DIV(col-sm-9)]
0854|- [DIV(navbar-header)][BUTTON_DEF]
0855|*TYPE="button" class="navbar-toggle collapsed" data-toggle="collapse" data-target="#navbar" aria-expanded="false" aria-controls="navbar"
0856|- [/BUTTON_DEF]
0857|- [SPAN(sr-only)]
0858|*Toggle navigation
0859|- [/SPAN][SPAN(icon-bar)]
0860|- [/SPAN][SPAN(icon-bar)]
0861|- [/SPAN][SPAN(icon-bar)]
0862|- [/SPAN][/BUTTON][LINK_DEF]
0863|*class="navbar-brand" href="index.html" rel="home" title="Survo help system"
0864|- [/LINK_DEF][IMAGE(img/survo-logo-small.png)][/IMAGE][/LINK]
0865|- [/DIV][DIV_DEF]
0866|*CLASS="navbar-collapse collapse" id="navbar" aria-expanded="false"
0867|- [/DIV_DEF][LIST_DEF]
0868|*CLASS="nav navbar-nav"
0869|- [/LIST_DEF][L][LINK(get-started.html)]
0870|*GET STARTED
S   |           |
0871|- [/L][L][LINK(commands.html)]
0872|*COMMANDS
S   |        |
0873|- [/L][L][LINK(examples.html)]
0874|*EXAMPLES
S   |        |
0875|- [/L][L][LINK(http://forum.survo.fi/)]
0876|*FORUM
S   |     |
0877|- [/L][/LIST][/DIV][/DIV][DIV_DEF]
0878|*CLASS="col-sm-3 pull-right"
0879|- [/DIV_DEF][FORM_DEF]
0880|*CLASS="navbar-form" ACTION="search.html"
0881|- [/FORM_DEF][DIV(input-group)][INPUT(q,text)]
0882|*CLASS="form-control"
0883|- [/INPUT][DIV(input-group-btn)][BUTTON_DEF]
0884|*CLASS="btn btn-default" TYPE="submit"
0885|- [/BUTTON_DEF]
0886|- [SPAN_DEF]
0887|*CLASS="glyphicon glyphicon-search"
0888|- [/SPAN_DEF][/SPAN][/BUTTON][/DIV][/DIV][/FORM]
0889|- [/DIV][/DIV][/DIV][/NAV][DIV(container)]
0890|A
0891|*End which incl. footer
S   |3
0892|bDEF end,b+1,B-1
0893|- [FOOTER]
0894|/
0895|/
0896|- [P(pull-right)]
0897|- [LINK_DEF]
0898|*href="index.html" class="pagination-centered btn btn-default btn-sm"
0899|- [/LINK_DEF][SPAN_DEF]
0900|*class="glyphicon glyphicon-home"
0901|- [/SPAN_DEF][/SPAN][/LINK]
0902|- [LINK_DEF]
0903|*href="#" class="btn btn-default btn-sm"
0904|- [/LINK_DEF][SPAN_DEF]
0905|*class="glyphicon glyphicon-chevron-up"
0906|- [/SPAN_DEF][/SPAN]
0907|*Back to Top
S   |           |
0908|- [/LINK]
0909|/c 2016 Survo
0910|- [/FOOTER][/DIV]
0911|- / Put Javascript here at the end of the page
0912|- / Bootstrap and jQuery
0913|- [SCRIPT(https://ajax.googleapis.com/ajax/libs/jquery/1.12.4/jquery.min.js)][/SCRIPT]
0914|- [SCRIPT(js/bootstrap.min.js)]][/SCRIPT]
0915|- / Google analytics
0916|- [SCRIPT]
0917|*  (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
0918|*  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
0919|*  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
0920|*  })(window,document,'script','https://www.google-analytics.com/analytics.js','ga');
0921|*  ga('create', 'UA-81645810-1', 'auto');
0922|*  ga('send', 'pageview');
0923|- [/SCRIPT]
0924|- / Google search engine
0925|- [SCRIPT]
0926|* (function() {
0927|*   var cx = '010896179039517952376:nay7kuxzphw';
0928|*   var gcse = document.createElement('script');
0929|*   gcse.type = 'text/javascript';
0930|*   gcse.async = true;
0931|*   gcse.src = 'https://cse.google.com/cse.js?cx=' + cx;
0932|*   var s = document.getElementsByTagName('script')[0];
0933|*   s.parentNode.insertBefore(gcse, s);
0934|* })();
0935|- [/SCRIPT]
0936|B
0938|*Sidebar
S   |3
0939|cDEF sidebar,c+1,C-1
0942|C
0943|*General parameters
S   |3
0944|dDEF select,d+1,D-1
0945|ISELECT='Boolean expression'
S   | xxxxxxxxxxxxxxxxxxxxxxxxxxx
0946|Nselects observations satisfying the conditions given by
0947|*Boolean expression=A+B+C+... (read A or B or C or ...) or
0948|*A*B*C*...  (read A and B and C and ...).
0949|*Each condition A,B,C,... must be given as a specification of the
0950|*form
0951|N     A1='variable','lower limit','upper limit'
0952|*or
0953|N     A1='string variable':'case1','case2',...
0954|NAny words can be used in place of A,B,C.
0956|NExample:
0957|*SELECT=North*Over10000+South*Over20000
S   | xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
0958|*North=Province:OUL,LAP South=Province:UUS,KYM,TUR
S   | xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
0959|*Over10000=Popul,10000,500000  Over20000=Popul,20000,500000
S   | xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
0960|Nselects cases where
0961|*    either  Province is OUL or LAP         and  Popul>=10000
0962|*    or      Province is UUS or KYM or TUR  and  Popul>=20000
0964|NThe SELECT specification can also be written with negations
0965|*(a `!' in front of a Boolean expression)
0966|*and with parentheses.
0967|*For details read more about
0968|- [LINK(#)]
0969|*select observations and/ or variables.
S   |                                     |
0970|D
0971|fDEF vars,f+1,F-1
0972|IVARS='list of variables'
S   | xxxxxxxxxxxxxxxxxxxxxxxx
0973|Nis a specification which selects the variables into statistical analysis.
0974|*Another way to do that is to use a MASK specification, but VARS always
S   |                                    xxxx                    xxxx
0975|*overrides MASK and permanent activation status in the data file.
S   |           xxxx
0976|NFor example,
0977|*VARS=Height,Weight,Test1
S   | xxxxxxxxxxxxxxxxxxxxxxxx
0978|*selects the variables Height,Weight and Test1 which all are activated
0979|*by 'A' in the current data file; other variables will be passive.
0980|*VARS=ALL means that all variables are to be selected.
S   | xxxxxxxx
0981|*VARS=ALL,-Name,-Test1 means that all variables except Name and Test1 are
S   | xxxxxxxxxxxxxxxxxxxxx
0982|*to be selected.
0983|*The task of each variable can be given in parentheses in the VARS list.
0984|NFor example, in the LINREG operation
0985|*VARS=Height(X),Weight(X),Test1(Y),Residual(R)
S   | xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
0986|*selects the variables and indicates their roles in the analysis as well.
0988|*The influence of VARS is only temporary and does not change the
0989|*activation status in the data file.
0990|F
0991|gDEF mask,g+1,G-1
0992|IMASK
S   | xxxx
0993|NMASK without parameters is an operation which leads to the activation
S   | xxxx
0994|*procedure for the fields of any Survo data file.
0996|*After return from MASK procedure to normal edit mode the current
0997|*line will contain a MASK specification of the form
0998|*MASK=--XX-A-YX-R---AA- (for example)
S   | xxxxxxxxxxxxxxxxxxxxxx
0999|*indicating the activated fields in the current data file.
1000|*If a MASK specification appears in the current subfield,
1001|*when a statistical operation like CORR,LINREG,TAB etc. is activated,
1002|*it determines the roles of the variables in the analysis.
1003|*However, a possible VARS specification always overrides MASK in
1004|*statistical operations.
1006|*By activation of a MASK line with a given mask string (MASK=--XX-A-- ,
1007|*for example) the selection of the fields determined by that string will
1008|*be shown for a selected file and this selection may be adjusted. After
1009|*return to edit mode again the MASK line will show the latest selection.
1011|*Since a data file can contain several optional mask columns (default
1012|*number is 7), any of them can be used to indicate the selected vari-
1013|*ables by giving its number #1,#2,#3,,, as MASK=#7.
1014|*A partial selection according to a mask column is also possible. For
1015|*example, MASK=#7(XY) selects the fields indicated by X or Y in mask
S   |          xxxxxxxxxxx
1016|*column 7.
1017|*Activation of a MASK line of this type will lead to a file activation
1018|*display of that mask column for additional changes.
1019|G
1020|*List of general parameters
1021|*SELECT
1022|*VARS
1023|*MASK
1024|*IND   (yksityiskohta selecting observations)
1025|*CASES (yksityiskohta selecting observations)
1026|*PRIND
1027|*RESULTS
1028|*SIMUMAX
1029|*OUTFILE
1030|*ITERATIONS
1031|*RND
1032|*SEED
1033|*WEIGHT
1034|*WEIGHTS
1035|*METHOSD
1036|*RAND
1037|*VARIABLES
1038|*EPS
1039|*MATCH
1040|*LABEL
1041|*TYPE
1042|*JITTER
1043|*CONSTANT
1044|*CONSTANTS
1045|*FORMAT
1046|*DELIMITER
1047|*MISSING
1048|*MODE
1049|*SIZE
1050|*SORT
1051|*CONF
1052|*TEST
1053|*MIN
1054|*MAX
1056|*........................................................................................................................................................................................................
1057|* - PRINT WEB CONTENT
S   |4
1058|+
1059|*LOAD MISC
1060|*LOAD FILE
1061|*LOAD ...
